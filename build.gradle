plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id "me.modmuss50.mod-publish-plugin" version "0.5.1"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	maven { url = "https://jitpack.io" }
	maven { url = "https://api.modrinth.com/maven" }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "maven.modrinth:betterf3:${project.betterf3_version}" // not required; broken JiJ, should work anyways
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishMods {
	file = remapJar.archiveFile
	changelog = "Updated to remove MixinExtras dependency"
	type = STABLE
	modLoaders.add("fabric")

	modrinth {
		projectId = "WnMjHMA3"
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		minecraftVersionRange {
			start = "1.19"
			end = "1.20.6"
			includeSnapshots = true
		}
		optional("betterf3")
	}
	github {
		repository = "Benonardo/Overworld-Coordinates"
		accessToken = providers.environmentVariable("GITHUB_TOKEN")
		commitish = "main"
	}
}